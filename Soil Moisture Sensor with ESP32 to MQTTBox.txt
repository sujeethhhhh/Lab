#include <WiFi.h>
#include <PubSubClient.h>

// Soil moisture sensor pin
#define SOIL_PIN 35

// WiFi credentials
const char* ssid = "name";
const char* password = "password";

// MQTT broker details
const char* mqtt_server = "broker.emqx.io";
const int mqtt_port = 1883;
const char* client_id = "db032482-2f62-4ba8-9782-da6521020775";
const char* topic = "quantanics/industry/testing1";

WiFiClient espClient;
PubSubClient client(espClient);

void setup() {
    Serial.begin(115200);
    pinMode(SOIL_PIN, INPUT);

    // Connect to WiFi
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
        delay(1000);
        Serial.println("Connecting to WiFi...");
    }
    Serial.println("WiFi connected");

    // Setup MQTT server
    client.setServer(mqtt_server, mqtt_port);
}

void reconnect() {
    while (!client.connected()) {
        Serial.println("Connecting to MQTT...");
        if (client.connect(client_id)) {
            Serial.println("Connected to MQTT broker");
        } else {
            Serial.print("Failed, rc=");
            Serial.print(client.state());
            Serial.println(" Retrying in 5 seconds...");
            delay(5000);
        }
    }
}

void loop() {
    if (!client.connected()) {
        reconnect();
    }
    client.loop();

    // Read soil moisture
    int soilValue = analogRead(SOIL_PIN);

    // Create message string
    String payload = "Soil Moisture: " + String(soilValue);

    // Publish and print
    client.publish(topic, payload.c_str());
    Serial.println(payload);

    delay(1000); // Wait before next reading
}